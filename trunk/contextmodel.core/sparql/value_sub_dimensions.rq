# Parameters:
# 1 - value individual

PREFIX cmv:		<http://dei.polimi.it/dbgroup/pedigree/context-model-vocabulary.owl#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX list:	<http://jena.hpl.hp.com/ARQ/list#>

SELECT ?actualDimensionClass ?valuesClass ?formalDimensionIndividual
WHERE 
{
	?parentValueRestriction owl:hasValue <%1$s> .
	?parentValueRestriction rdf:type owl:Restriction .
	?parentValueRestriction owl:onProperty cmv:dimensionValue .
	?parentDimensionIntersection list:member ?parentValueRestriction .
	?parentDimensionRestrictionClass owl:intersectionOf ?parentDimensionIntersection .
	
	?parentDimensionRestriction owl:someValuesFrom ?parentDimensionRestrictionClass .
	?parentDimensionRestriction rdf:type owl:Restriction .
	?parentDimensionRestriction owl:onProperty cmv:hasDimension .
	
	?contextRestriction owl:someValuesFrom ?parentDimensionRestriction .
	?contextRestriction rdf:type owl:Restriction .
	?contextRestriction owl:onProperty cmv:inContext .
	
	?actualDimensionClass rdfs:subClassOf ?contextRestriction .
	?actualDimensionClass rdfs:subClassOf cmv:ActualDimension .
	
	?actualDimensionClass rdfs:subClassOf ?formalDimensionRestriction .
	?formalDimensionRestriction rdf:type owl:Restriction .
	?formalDimensionRestriction owl:onProperty cmv:formalDimension .
	?formalDimensionRestriction owl:hasValue ?formalDimensionIndividual .
	
	?actualDimensionClass rdfs:subClassOf ?dimensionValueRestriction .
	?dimensionValueRestriction rdf:type owl:Restriction .
	?dimensionValueRestriction owl:onProperty cmv:dimensionValue .
	?dimensionValueRestriction owl:someValuesFrom ?valuesClass .
	
	FILTER( ?valuesClass != cmv:Value )
}