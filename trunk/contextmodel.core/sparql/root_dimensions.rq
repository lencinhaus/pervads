PREFIX cmv:      <http://dei.polimi.it/dbgroup/pedigree/context-model-vocabulary.owl#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?actualDimensionClass ?valuesClass ?formalDimensionIndividual
WHERE 
{ 
	?actualDimensionClass rdfs:subClassOf cmv:ActualDimension .
	
	?actualDimensionClass rdfs:subClassOf ?formalDimensionRestriction .
	?formalDimensionRestriction rdf:type owl:Restriction .
	?formalDimensionRestriction owl:onProperty cmv:formalDimension .
	?formalDimensionRestriction owl:hasValue ?formalDimensionIndividual .
	
	?actualDimensionClass rdfs:subClassOf ?dimensionValueRestriction .
	?dimensionValueRestriction rdf:type owl:Restriction .
	?dimensionValueRestriction owl:onProperty cmv:dimensionValue .
	?dimensionValueRestriction owl:someValuesFrom ?valuesClass .
	
	FILTER(
		?valuesClass != cmv:Value &&
		NOT EXISTS{
			?actualDimensionClass rdfs:subClassOf ?contextRestriction .
			?contextRestriction rdf:type owl:Restriction .
			?contextRestriction owl:onProperty cmv:inContext .
			?contextRestriction owl:someValuesFrom ?parentDimensionRestriction .
			?parentDimensionRestriction rdf:type owl:Restriction .
			?parentDimensionRestriction owl:onProperty cmv:hasDimension
		}
	)
}